#!/usr/bin/env perl
use strict;
use File::Slurp;
use File::Basename;
use constant { true => 1, false => 0 };

#: Replace these values with the official signing key signatures so that offline
#: signing can be performed without requiring a complete build of the OS.
our %OFFICIAL_SIGS =
  ( bullhead => "A78DAF79F658BA3242ADE851637FEA184AF53DAF306C9131209FC5DC634A9396",
    angler   => "0E39376C3DFD4DE49EDEBD0E4629CF94B9C40C01D5B284B2112F2FF088FAD351",
    sailfish => "2C679B2951D7D1D54DA1534AE8B7FF1681B195806E34985D040C9B9D6CB48874",
    marlin   => "6E461BDB712AC6A0CC3251B1DFEDD96C367E814116D9816D550E9DB606C1F628",
    walleye  => "B2D735F2B2BCA4DB548BBD606776AACD81AFE5430BC24C369D151D1AAF993D9F",
    taimen   => "03F03912587E3DD68AB91F9165D35AFB04DF38BBFC38161C0D2592A6D385676F"
  );

our %PATH =
  ( 'keys' => './keys',
    'fdpe' => 'packages/apps/F-Droid/privileged-extension/app/src/main/java/org/fdroid/fdroid/privileged/ClientWhitelist.java'
  );

fatal("keys directory not found")
  unless (-d $PATH{keys});
fatal("ClientWhitelist.java not found")
  unless (-f $PATH{fdpe});

our $USE_OFFICIAL_SIGS = false;
foreach my $arg (@ARGV) {
  if ($arg =~ m!^(\-h|\-\-help)$!) {
    stderr("usage: ".basename($0)." [-h] [-O]");
    stderr("\noptions:");
    stderr("    -h         this helpful message");
    stderr("    -O         use official signatures");
    exit(1);
  }
  if ($arg =~ m!^(\-O|\-\-official)$!) {
    $USE_OFFICIAL_SIGS = true;
    last;
  }
}

our @DEVICES = qw(bullhead angler sailfish marlin walleye taimen);
my $RAW_FDPE = read_file($PATH{fdpe});

foreach my $device (@DEVICES) {
  my $sig = ($USE_OFFICIAL_SIGS) ? $OFFICIAL_SIGS{$device} : get_fingerprint($device);
  stdout($sig." ".$device);
  $RAW_FDPE =~ s!^(\s*new Pair<>\(".+?", ").+?("\s*\),??\s*//\s*\Q$device\E releasekey)\s*$!${1}${sig}${2}!m;
}

write_file($PATH{fdpe},$RAW_FDPE);

chdir('packages/apps/F-Droid/privileged-extension/');
system('git diff');
exit 0;

sub get_fingerprint {
  my ($device) = @_;
  my $pem_path = $PATH{keys}.'/'.$device.'/releasekey.x509.pem';
  fatal("key net found: ".$pem_path)
    unless (-f $pem_path);
  my $result = `keytool -list -printcert -file "${pem_path}" 2>/dev/null | grep 'SHA256:' | tr -d ':' | cut -d ' ' -f 3`;
  chomp($result);
  return $result;
}

sub stdout {
  foreach (@_) {
    print STDOUT $_."\n";
  }
}

sub stderr {
  foreach (@_) {
    print STDERR $_."\n";
  }
}

sub fatal {
  stderr(@_);
  exit 1;
}
